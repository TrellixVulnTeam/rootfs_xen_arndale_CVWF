#!/usr/bin/perl -w
# menu-files -- lintian collector script

# Copyright (C) 1998 Richard Braakman
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, you can find it on the World Wide
# Web at http://www.gnu.org/copyleft/gpl.html, or write to the Free
# Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston,
# MA 02110-1301, USA.

package Lintian::coll::menu_files;

use strict;
use warnings;
use autodie;

use lib "$ENV{'LINTIAN_ROOT'}/lib";
use Lintian::Util qw(copy_dir delete_dir fail is_ancestor_of);

sub collect {
    my ($pkg, $type, $dir) = @_;

    if (-e "$dir/menu") {
        delete_dir("$dir/menu")
          or fail('cannot rm old menu directory');
    }

    # If we are asked to only remove the files stop right here
    if ($type =~ m/^remove-/) {
        return;
    }

    mkdir("$dir/menu", 0777);

    my %dirs;
    $dirs{'share'}   = 'usr/share/menu';
    $dirs{'lib'}     = 'usr/lib/menu';
    $dirs{'methods'} = 'etc/menu-methods';

    while (my ($shortn, $path) = each %dirs) {
        if (-d "$dir/unpacked/$path") {
            if (!is_ancestor_of("$dir/unpacked", "$dir/unpacked/$path")) {
                # Unsafe, skip
                mkdir("$dir/menu/$shortn", 0777);
                next;
            }
            copy_dir("$dir/unpacked/$path", "$dir/menu/$shortn")
              or fail("cannot copy unpacked/$path/ directory");
        } else {
            # no menu directory
            mkdir("$dir/menu/$shortn", 0777);
        }
    }

    return;
}

collect(@ARGV) if $0 =~ m,(?:^|/)menu-files$,;

1;

# Local Variables:
# indent-tabs-mode: nil
# cperl-indent-level: 4
# End:
# vim: syntax=perl sw=4 sts=4 sr et
